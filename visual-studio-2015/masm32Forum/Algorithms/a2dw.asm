 .486
 .model flat, stdcall
 option casemap:none

 ; -----------------------------------------------------------------------
  ; This version is a drop in replacement for Iczelion's original algorithm
  ; -----------------------------------------------------------------------
    a2dw PROTO :DWORD

    .code       ; code section

; ??????????????????????????????????????????????????????????????????????????????????????????????????

OPTION PROLOGUE:NONE
OPTION EPILOGUE:NONE

align 4

a2dw proc String:DWORD

  ; ------------------------------------------------
  ; Convert decimal string into UNSIGNED DWORD value
  ; ------------------------------------------------

    mov edx, [esp+4]
    xor ecx, ecx
    movzx eax, BYTE PTR [edx]
    test eax, eax
    jz quit

  lpst:
    add edx, 1
    lea ecx, [ecx+ecx*4]            ; mul ecx * 5
    lea ecx, [eax+ecx*2-48]
    movzx eax, BYTE PTR [edx]
    test eax, eax
    jz quit

    add edx, 1
    lea ecx, [ecx+ecx*4]            ; mul ecx * 5
    lea ecx, [eax+ecx*2-48]
    movzx eax, BYTE PTR [edx]
    test eax, eax
    jnz lpst

  quit:
    lea eax, [ecx]

    ret 4

a2dw endp

OPTION PROLOGUE:PrologueDef
OPTION EPILOGUE:EpilogueDef

; ??????????????????????????????????????????????????????????????????????????????????????????????????

end
